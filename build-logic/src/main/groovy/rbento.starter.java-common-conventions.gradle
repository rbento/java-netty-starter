plugins {
	id 'java'
	id 'idea'
	id 'io.freefair.lombok'
	id 'net.ltgt.errorprone'
	id 'com.diffplug.spotless'
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	errorprone libs.errorprone.config
	implementation libs.slf4j.simple
	testImplementation libs.assertj.core
	testImplementation libs.junit.jupiter
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
		vendor = JvmVendorSpec.AZUL
	}
}

spotless {
	encoding 'UTF-8'
	java {
		target 'src/*/java/**/*.java'
		targetExclude "${project.layout.buildDirectory}/**"
		importOrder()
		removeUnusedImports()
		palantirJavaFormat(libs.versions.palantir.java.format.get())
		formatAnnotations()
		endWithNewline()
		licenseHeaderFile "${project.rootDir}/COPYRIGHT"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
	testLogging {
		events "PASSED", "FAILED", "SKIPPED"
	}
}

compileJava.dependsOn 'spotlessApply'
